システム!"MBit"使う.
MB=MBit!作る.
MB!（システム！シリアルポート選択）開く．

 // 壁を作る（ステップ1）
 かめた＝タートル！ 作る。
 かめた！（緑）線の色 20 線の太さ。
 かめた！500 歩く 90 右回り 420 歩く 90 右回り 500 歩く。
 壁＝かめた！図形を作る -200 200 位置。
 かめた！90 右回り。
 左壁＝かめた！（黄）線の色 440 歩く 図形を作る -210 -230 位置。
 パドル＝かめた！（青）線の色 120 歩く 図形を作る -189 -210 位置。

 // パドルを動かす（ステップ2）
 /*
 上ボタン＝ボタン！ "上" "UP" 作る -380 50 位置。
 下ボタン＝ボタン！ "下" "DOWN" 作る -380 0 位置。
 上ボタン：動作＝「パドル！ 0 50 移動する」。
 下ボタン：動作＝「パドル！ 0 -50 移動する」。
 */
 タイマー！作る　0.2　間隔　120000　回数「
 　　Y=(MB!"Y"読む)/-4.
 　　パドル！-189 (Y) 位置
 」実行．

 // ボールを動かす（ステップ3）
 かめた！ ペンなし。
 かめた！（乱数（200））（乱数（300）-150）位置。
 かめた！45 向き。
 かめた：衝突＝タートル：跳ね返る。
 時計＝タイマー！ 作る 60秒 時間「かめた！15 歩く」実行。

 // ゲームの勝敗を判定する（ステップ4）
 ゲームクリア＝はい。
 左壁：衝突＝「：ゲームクリア＝いいえ。時計！ 中断」。
 時計！「
 　　「ゲームクリア＝＝はい」！ なら「
 　　　　ラベル！ "ゲームクリア！ "作る （青）文字色。
 　　」そうでなければ「
 　　　　ラベル！ "ゲームオーバー！ "作る （赤）文字色。
 　　」実行。
 」最後に実行。