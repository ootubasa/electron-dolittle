システム!"MBit"使う.
MB=MBit!作る.
MB!（システム！シリアルポート選択）開く．


 // 主役と左右の動き（ステップ1）
 かめた＝タートル！作る 90 左回り ペンなし 0 -150 位置。
 /*
 左=ボタン！"左" "LEFT" 作る -200 -180 位置。
 左:動作＝「かめた！-20 0 移動する」。
 右=ボタン！"右" "RIGHT" 作る 50 -180 位置。
 右:動作＝「かめた！20 0 移動する」。
 */
 MB!"Aボタン"「かめた！-20 0 移動する」動作設定．
 MB!"Bボタン"「かめた！20 0 移動する」動作設定．
 
 // 発射ボタン（ステップ2）
 かめた：発射＝「タイマー！作る「自分！20 歩く」実行」。
 /*
 発射=ボタン！"発射" "UP" 作る -50 -180 位置 100 45 大きさ。
 発射:動作＝「かめた！作る 55 歩く "rocket.gif" 変身する 発射」。
 */
 MB!"ABボタン"「かめた！作る 55 歩く "rocket.gif" 変身する 発射」動作設定．
 
 // 敵たちの生成（ステップ3）
 敵たち＝配列！作る。
 敵＝タートル！作る "ayumiAka.gif" 変身する ペンなし 90 右回り。
 敵たち！（敵！-300 200 位置）書く。
 敵たち！（敵！作る -200 200 位置）書く。
 敵たち！（敵！作る -100 200 位置）書く。
 敵たち！（敵！作る 0 200 位置）書く。
 敵たち！（敵！作る 100 200 位置）書く。
 
 // 敵たちの移動（ステップ4）
 時計＝タイマー！作る 1秒 間隔 。
 時計!6 回数「敵たち！「｜敵|敵！30 0 移動する」それぞれ実行」実行。
 時計!1 回数「敵たち！「｜敵|敵！0 -30 移動する」それぞれ実行」実行。
 時計!6 回数「敵たち！「｜敵|敵！-30 0 移動する」それぞれ実行」実行。
 
 // 衝突定義（ステップ5）
 かめた：衝突＝「自分！消える」。
 敵：衝突＝「自分！消える。敵たち！（自分）消す」。
 
 // 終了判定（ステップ6）
 制限時間＝15。
 カウントダウン=フィールド！作る 150 0 移動する。
 終了時計＝タイマー！作る 1秒 間隔（制限時間）時間。
 終了時計！「｜n|カウントダウン！（制限時間-n）書く。
 　「（敵たち！要素数?） ＝＝ 0」！なら「
 　　ラベル！"おめでとう！！！"作る -100 200 位置。
 　　終了時計！中断
 　」実行
 」実行。